
&НаКлиенте
Процедура ВыполнитьСценарий(Команда)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		Сценарий = Элементы.Список.ТекущаяСтрока;
		Если Сценарий = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если ТЦСервер.БСППодсистемаИспользуется(ТЦОбщий.БСПИмяПодсистемыУправленияДоступом()) И Не ТЦСервер.БСПРольДоступна("ПолныеПрава") Тогда
			Предупреждение("В конфигурации используется подсистема управления доступом БСП.
							|Для корректной работы Тест-центра управляющее подключение должно быть запущено под пользователем с ролью ""ПолныеПрава"".");
			Возврат;
		КонецЕсли;
		НеобходимыеРоли = ТЦСервер.ПодобратьПраваДляПодсистемы("ТестЦентр");
		Если ЗначениеЗаполнено(НеобходимыеРоли) Тогда
			ДобавляемыеРоли = ТЦСервер.ПроверитьПраваОригинальныхПользователей(Сценарий, НеобходимыеРоли);
			Если ЗначениеЗаполнено(ДобавляемыеРоли) Тогда
				КодВозврата = Вопрос("Для корректной работы Тест-центра у пользователей участвующих в сценарии должны быть установлены следующие роли: " + "
									  |" + ТЦОбщий.МассивВСтроку(ДобавляемыеРоли) + "
									  |Установить их автоматичестки?" , РежимДиалогаВопрос.ДаНетОтмена);
				Если КодВозврата = КодВозвратаДиалога.Да Тогда
					ТЦСервер.УстановитьПраваОргинальныхПользователей(Сценарий, ДобавляемыеРоли);
				ИначеЕсли КодВозврата = КодВозвратаДиалога.Отмена Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ТЦСервер.ВыполнитьСценарий(Сценарий);
		ТЦКлиент.ПоказатьСостояниеТестирования();
		Элементы.Выполнить.Доступность = Ложь;
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
	КонецПопытки;
	
КонецПроцедуры
