&НаКлиенте
Процедура Настроить(Команда)
	
	Попытка
		ДатаНачала = ТекущаяДата();
		ИмяФормыОбработки = ТЦКлиент.ПолучитьИмяФормыОбработки(Объект.Обработка);
		П = Новый Структура;
		П.Вставить("ТЦСценарий", Параметры.ТЦСценарий);
		П.Вставить("ТЦРоль", Объект.Ссылка);
		ФормаОбработки = ПолучитьФорму(ИмяФормыОбработки, П);
		ФормаОбработки.ТЦЗагрузить(ПолучитьПараметры());
		Результат = ФормаОбработки.ОткрытьМодально();
		
		Если Результат = Истина Тогда
			СохранитьПараметры(ФормаОбработки.ТЦСохранить());
		КонецЕсли;
	Исключение
		Ошибка = ИнформацияОбОшибке();
		ТЦОбщий.ЗаписатьВЖурнал(Ошибка);
		ТЦКлиент.СообщитьОбОшибке(Ошибка, ДатаНачала, ТекущаяДата());
	КонецПопытки;
	
	
КонецПроцедуры // Настроить()

&НаСервере
// Получить параметры обработки
//
// Возвращаемое значение:
//  ХранилищеЗначения
//
Функция ПолучитьПараметры()
	
	Возврат Объект.Ссылка.Параметры;
	
КонецФункции // ПолучитьПараметры()

&НаСервере
// Сохранить параметры обработки
//
// Параметры:
//  ПараметрыОбработки - ХранилищеЗначения
//
Процедура СохранитьПараметры(ПараметрыОбработки)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Параметры = ПараметрыОбработки;
	ТекущийОбъект.Записать();
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
КонецПроцедуры // СохранитьПараметры()

&НаКлиенте
Процедура ОбработкаПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
// Установить доступность элементов упрвления
//
Процедура УстановитьДоступность()
	
	Элементы.Настроить.Доступность = ЗначениеЗаполнено(Объект.Обработка);
	
КонецПроцедуры // УстановитьДоступность()

&НаКлиенте
// Обработчик открытия формы
//
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры // ПриОткрытии()
