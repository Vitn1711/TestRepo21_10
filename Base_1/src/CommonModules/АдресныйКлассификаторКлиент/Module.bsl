////////////////////////////////////////////////////////////////////////////////
// АдресныйКлассификатор: содержит алгоритмы работы с адресным классификатором, 
//   исполняемые на клиенте
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура ЗагрузитьАдресныйКлассификатор(Знач ПараметрыЗагрузки = Неопределено) Экспорт
	
	ОткрытьФорму("РегистрСведений.АдресныйКлассификатор.Форма.ЗагрузкаАдресногоКлассификатора", ПараметрыЗагрузки);
		
КонецПроцедуры

// Вызывает форму очистки сведений адресного классификатора по адресным объектам
//
// Параметры:
//     Владелец - ФормаКлиентскогоПриложения - форма, из которой осуществляется очистка адресного классификатора.
//
Процедура ОчиститьКлассификатор(Владелец = Неопределено) Экспорт
	
	ОткрытьФорму("РегистрСведений.АдресныйКлассификатор.Форма.ОчисткаАдресногоКлассификатора", , Владелец);
		
КонецПроцедуры

// Открывает форму выбора уровня - разрыв взаимозависимости систем
//
Функция ОткрытьФормуВыбораКАТО(ПараметрыФормы = Неопределено, Владелец = Неопределено, Уникальность = Неопределено, Окно = Неопределено, НавигационнаяСсылка = Неопределено, ОписаниеОповещенияОЗакрытии = Неопределено) Экспорт
	
	Возврат ОткрытьФорму("РегистрСведений.АдресныйКлассификатор.Форма.ФормаВыбора", ПараметрыФормы, Владелец, 
		Уникальность, Окно, НавигационнаяСсылка, ОписаниеОповещенияОЗакрытии);
		
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Вызывает диалог выбора каталога
// 
// Параметры:
//     Форма - ФормаКлиентскогоПриложения - вызывающий объект
//     ПутьКДанным          - Строка  - полное имя реквизита формы, содержащего значение каталога. Например "РабочийКаталог" 
//                                      или "Объект.КаталогИзображений"
//     Заголовок            - Строка - Заголовок для диалога 
//     СтандартнаяОбработка - Булево - для использования в обработчике "ПриНачалаВыбора". Будет заполнено значением Ложь
//
Процедура ВыбратьФайл(Знач Форма, Знач ПутьКДанным, Знач Заголовок = Неопределено, СтандартнаяОбработка = Ложь) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьФайлЗавершениеКонтроляРасширенияРаботыСФайлами", ЭтотОбъект, Новый Структура);
	Оповещение.ДополнительныеПараметры.Вставить("Форма",       Форма);
	Оповещение.ДополнительныеПараметры.Вставить("ПутьКДанным", ПутьКДанным);
	Оповещение.ДополнительныеПараметры.Вставить("Заголовок",   Заголовок);
	
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(Оповещение, , Ложь);
КонецПроцедуры

// Завершение немодального выбора каталога
//
Процедура ВыбратьФайлЗавершениеКонтроляРасширенияРаботыСФайлами(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат <> Истина Тогда
		// Отказ от установки расширения
		Возврат;
		
	ИначеЕсли Не ПодключитьРасширениеРаботыСФайлами() Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Расширение для работы с файлами не подключено.'"));
		Возврат;
	КонецЕсли;
	
	Форма       = ДополнительныеПараметры.Форма;
	ПутьКДанным = ДополнительныеПараметры.ПутьКДанным;
	Заголовок   = ДополнительныеПараметры.Заголовок;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Если Заголовок <> Неопределено Тогда
		Диалог.Заголовок = Заголовок;
	КонецЕсли;
	
	ВладелецЗначения = Форма;
	ТекущееЗначение  = Форма;
	ИмяРеквизита     = ПутьКДанным;
	
	ЧастиПути = СтрЗаменить(ПутьКДанным, ".", Символы.ПС);
	Для Позиция = 1 По СтрЧислоСтрок(ЧастиПути) Цикл
		ИмяРеквизита     = СтрПолучитьСтроку(ЧастиПути, Позиция);
		ВладелецЗначения = ТекущееЗначение;
		ТекущееЗначение  = ТекущееЗначение[ИмяРеквизита];
	КонецЦикла;
	
	Диалог.ПолноеИмяФайла = ТекущееЗначение;
	
	Фильтр = НСтр("ru = 'XML-данные'; en = 'eXtensible Markup Language'") + "(*.xml)|*.xml";

	Диалог.Фильтр = Фильтр;
	
	Если Диалог.Выбрать() Тогда
		ВладелецЗначения[ИмяРеквизита] = Диалог.ПолноеИмяФайла;
		
		СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(Диалог.ПолноеИмяФайла);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			ВладелецЗначения.Элементы, "Кодировка", "Видимость", ВРег(СтруктураИмениФайла.Расширение) = ".DBF");
			
		ДанныеВыбора = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

Функция МожноИзменятьАдресныйКлассификатор()
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента();
	Возврат НЕ ПараметрыРаботыКлиента.РазделениеВключено;
	
КонецФункции
